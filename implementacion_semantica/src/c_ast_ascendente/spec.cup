package c_ast_ascendente;

import java_cup.runtime.*;
import asint.ClaseSemanticaEval;
import c_ast_ascendente.UnidadLexica.StringLocalizado;
import asint.SintaxisAbstractaEval.*;

scan with {: 
  return getScanner().next_token(); 
:};
parser code {: 
   private GestionErroresEval errores;
   public void syntax_error(Symbol unidadLexica) {
     errores.errorSintactico((UnidadLexica)unidadLexica);
   }
:};
init with {: 
   errores = new GestionErroresEval();
   AnalizadorLexico alex = (AnalizadorLexico)getScanner();
   alex.fijaGestionErrores(errores);
:};
action code {:
  ClaseSemanticaEval sem = new ClaseSemanticaEval();
:}

terminal      INT, REAL, BOOL, STRING, PROC, IF, ELSE, WHILE, STRUCT, NEW, DELETE, 
              READ, WRITE, NL, TYPE, CALL, PARENTESIS_APERTURA, EVAL, 
              PARENTESIS_CIERRE, COMA, PUNTO_Y_COMA, CORCHETE_CIERRE, PUNTO, 
              LLAVE_APERTURA, LLAVE_CIERRE, REFERENCIA, CAMBIO_DE_SECUENCIA;
terminal      StringLocalizado IDENTIFICADOR, LITERAL_ENTERO, LITERAL_REAL, LITERAL_CADENA, NULL, TRUE, FALSE,
              CORCHETE_APERTURA, PUNTERO, SUMA, RESTA, MULTIPLICACION, DIVISION, MODULO, AND, OR, NOT,MENOR,
              MAYOR, MENOR_O_IGUAL, MAYOR_O_IGUAL, IGUALDAD, DISTINTO, ASIG;

non terminal Prog programa;
non terminal Decs declaraciones_opt;
non terminal LDecs lista_declaraciones;
non terminal Dec declaracion, declaracion_variable, declaracion_tipo, declaracion_proc;
non terminal ParamF parametros_formales;
non terminal LParam lista_parametros;
non terminal Param parametro;
non terminal Tipo tipo, tipo1, tipo2, tipo_struct;
non terminal LStruct lista_struct;
non terminal Campo campo;
non terminal InstrOpt instrucciones_opt;
non terminal LInstr lista_instrucciones;
non terminal Instr instruccion, instruccion_eval, instruccion_if, instruccion_while, instruccion_read, instruccion_write, instruccion_nl, instruccion_reserva, instruccion_libera, instruccion_call, instruccion_compuesta;
non terminal ParamR parametros_reales;
non terminal LParamR lista_parametros_reales;
non terminal Exp E0, E1, E2, E3, E4, E5, E6, E7, opnd;
non terminal StringLocalizado op_relacional, op_nivel4, op_nivel5;

programa ::= LLAVE_APERTURA declaraciones_opt: decs instrucciones_opt:instrOpt LLAVE_CIERRE
    {: RESULT = sem.prog(decs, instrOpt); :};

declaraciones_opt ::= lista_declaraciones: lDecs CAMBIO_DE_SECUENCIA
    {: RESULT = sem.si_decs(lDecs); :};
declaraciones_opt ::=
    {: RESULT = sem.no_decs(); :};

lista_declaraciones	::= lista_declaraciones: lDecs PUNTO_Y_COMA declaracion: dec
    {: RESULT = sem.muchas_decs(lDecs, dec); :};
lista_declaraciones	::= declaracion: dec
    {: RESULT = sem.una_dec(dec); :};

declaracion ::= declaracion_variable: dec
    {: RESULT = dec; :};
declaracion ::= declaracion_tipo: dec
    {: RESULT = dec; :};
declaracion ::= declaracion_proc: dec
    {: RESULT = dec; :};

declaracion_variable ::= tipo: tipo IDENTIFICADOR: id
    {: RESULT = (Dec) sem.dec_id(tipo, id.str()).ponFila(id.fila()).ponCol(id.col()); :};

declaracion_tipo	::= TYPE tipo: tipo IDENTIFICADOR: id
    {: RESULT = (Dec) sem.dec_type(tipo, id.str()).ponFila(id.fila()).ponCol(id.col()); :};

declaracion_proc	::= PROC IDENTIFICADOR: id PARENTESIS_APERTURA parametros_formales: paramF PARENTESIS_CIERRE programa: prog
    {: RESULT = (Dec) sem.dec_proc(id.str(), paramF, prog).ponFila(id.fila()).ponCol(id.col()); :};

parametros_formales	::= lista_parametros: lParam
    {: RESULT = sem.si_parF(lParam); :};
parametros_formales ::= 
    {: RESULT = sem.no_parF(); :};

lista_parametros ::= lista_parametros: lParam COMA parametro: param
    {: RESULT = sem.muchos_param(lParam, param); :};
lista_parametros ::= parametro: param
    {: RESULT = sem.un_param(param); :};

parametro ::= tipo: tipo IDENTIFICADOR: id
    {: RESULT = (Param) sem.param_cop(tipo, id.str()).ponFila(id.fila()).ponCol(id.col()); :};
parametro ::= tipo: tipo REFERENCIA IDENTIFICADOR: id
    {: RESULT = (Param) sem.param_ref(tipo, id.str()).ponFila(id.fila()).ponCol(id.col()); :};

tipo	::= tipo: tipo CORCHETE_APERTURA LITERAL_ENTERO: id CORCHETE_CIERRE
    {: RESULT = (Tipo) sem.tipo_array(tipo, id.str()).ponFila(id.fila()).ponCol(id.col()); :};
tipo 	::= tipo1: tipo
    {: RESULT = tipo; :};
tipo1	::= tipo2: tipo
    {: RESULT = tipo; :};
tipo1	::= PUNTERO tipo1: tipo
    {: RESULT = sem.tipo_punt(tipo); :};
tipo1	::= INT
    {: RESULT = sem.tipo_int(); :};
tipo1	::= REAL
    {: RESULT = sem.tipo_real(); :};
tipo1	::= BOOL
    {: RESULT = sem.tipo_bool(); :};
tipo1	::= STRING
    {: RESULT = sem.tipo_string(); :};
tipo1	::= IDENTIFICADOR: id
    {: RESULT = (Tipo) sem.tipo_id(id.str()).ponFila(id.fila()).ponCol(id.col()); :};
tipo1	::= tipo_struct: tipo
    {: RESULT = tipo; :};


tipo_struct	::= STRUCT LLAVE_APERTURA lista_struct: LStruct LLAVE_CIERRE
    {: RESULT = sem.tipo_struct(LStruct); :};
lista_struct 	::= lista_struct: LStruct COMA campo: campo
    {: RESULT = sem.lista_struct(LStruct, campo); :};
lista_struct 	::= campo: campo
    {: RESULT = sem.info_struct(campo); :};
campo	::= tipo: tipo IDENTIFICADOR: id
    {: RESULT = (Campo) sem.campo(tipo, id.str()).ponFila(id.fila()).ponCol(id.col()); :};

instrucciones_opt	::= lista_instrucciones: lInstr
    {: RESULT = sem.si_inst(lInstr); :};
instrucciones_opt	::= 
    {: RESULT = sem.no_inst(); :};

lista_instrucciones	::= lista_instrucciones: lInstr PUNTO_Y_COMA instruccion: instr
    {: RESULT = sem.muchas_instr(lInstr, instr); :};
lista_instrucciones	::= instruccion: instr
    {: RESULT = sem.una_instr(instr); :};

instruccion	::= instruccion_eval: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_if: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_while: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_read: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_write: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_nl: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_reserva: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_libera: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_call: instr
    {: RESULT = instr; :};
instruccion	::= instruccion_compuesta : instr
    {: RESULT = instr; :};

instruccion_eval	::= EVAL E0: exp
    {: RESULT = sem.instr_eval(exp); :};
instruccion_if	::= IF E0: exp programa: prog
    {: RESULT = sem.instr_if(exp, prog); :};
instruccion_if	::= IF E0: exp programa: prog1 ELSE programa: prog2
    {: RESULT = sem.instr_else(exp, prog1, prog2); :};
instruccion_while	::= WHILE E0: exp programa: prog
    {: RESULT = sem.instr_wh(exp, prog); :};
instruccion_read	::= READ E0: exp
    {: RESULT = sem.instr_rd(exp); :};
instruccion_write	::= WRITE E0: exp
    {: RESULT = sem.instr_wr(exp); :};
instruccion_nl	::= NL
    {: RESULT = sem.instr_nl(); :};
instruccion_reserva	::= NEW E0: exp
    {: RESULT = sem.instr_new(exp); :};
instruccion_libera	::= DELETE E0: exp
    {: RESULT = sem.instr_del(exp); :};
instruccion_call		::= CALL IDENTIFICADOR: id PARENTESIS_APERTURA parametros_reales: paramR PARENTESIS_CIERRE
    {: RESULT = (Instr) sem.instr_call(id.str(), paramR).ponFila(id.fila()).ponCol(id.col()); :};
parametros_reales		::= lista_parametros_reales: lParamR
    {: RESULT = sem.si_param_re(lParamR); :};
parametros_reales		::=  
    {: RESULT = sem.no_param_re(); :};
lista_parametros_reales	::= lista_parametros_reales: lParamR COMA E0: exp
    {: RESULT = sem.muchos_param_re(lParamR, exp); :};
lista_parametros_reales	::= E0: exp
    {: RESULT = sem.un_param_re(exp); :};

instruccion_compuesta 	::= programa: prog
    {: RESULT = sem.instr_comp(prog); :};


E0 ::= E1: exp1 ASIG E0: exp2
    {: RESULT = sem.asig(exp1, exp2); :};
E0 ::= E1: exp
 {: RESULT = exp; :};
E1 ::= E1: exp1 op_relacional: op E2: exp2
    {: RESULT = sem.op_rel(op.str(), exp1, exp2); :};
E1 ::= E2: exp
 {: RESULT = exp; :};
E2 ::= E2: exp1 SUMA E3: exp2
    {: RESULT = sem.suma(exp1, exp2); :};
E2 ::= E3: exp1 RESTA E3: exp2
    {: RESULT = sem.resta(exp1, exp2); :};
E2 ::= E3: exp
 {: RESULT = exp; :};
E3 ::= E4: exp1 AND E3: exp2
    {: RESULT = sem.and(exp1, exp2); :};
E3 ::= E4: exp1 OR E4: exp2
    {: RESULT = sem.or(exp1, exp2); :};
E3 ::= E4: exp
 {: RESULT = exp; :};
E4 ::= E4: exp1 op_nivel4: op E5: exp2
    {: RESULT = sem.op_mul(op.str(), exp1, exp2); :};
E4 ::= E5: exp
 {: RESULT = exp; :};
E5 ::= op_nivel5: op E5: exp
    {: RESULT = sem.op_inv(op.str(), exp); :};
E5 ::= E6: exp
 {: RESULT = exp; :};
E6 ::= E6: exp1 CORCHETE_APERTURA E0: exp2 CORCHETE_CIERRE
  {: RESULT = sem.index(exp1, exp2); :};
E6 ::= E6: exp PUNTO IDENTIFICADOR: id
  {: RESULT = (Exp) sem.reg(exp, id.str()).ponFila(id.fila()).ponCol(id.col()); :};
E6 ::= E6: exp PUNTERO
  {: RESULT = sem.indir(exp); :};
E6 ::= E7: exp
 {: RESULT = exp; :};
E7 ::= PARENTESIS_APERTURA E0 PARENTESIS_CIERRE;
E7 ::= opnd: op
    {: RESULT = op; :};


op_relacional ::= MAYOR: op
    {: RESULT = new StringLocalizado(">", op.fila(), op.col()); :};
op_relacional ::= MAYOR_O_IGUAL: op
    {: RESULT = new StringLocalizado(">=", op.fila(), op.col()); :};
op_relacional ::= MENOR: op
    {: RESULT = new StringLocalizado("<", op.fila(), op.col()); :};
op_relacional ::= MENOR_O_IGUAL: op
    {: RESULT = new StringLocalizado("<=", op.fila(), op.col()); :};
op_relacional ::= IGUALDAD: op
    {: RESULT = new StringLocalizado("==", op.fila(), op.col()); :};
op_relacional ::= DISTINTO: op
    {: RESULT = new StringLocalizado("!=", op.fila(), op.col()); :};

op_nivel4 ::= MULTIPLICACION: op
    {: RESULT = new StringLocalizado("*", op.fila(), op.col()); :};
op_nivel4 ::= DIVISION: op
    {: RESULT = new StringLocalizado("/", op.fila(), op.col()); :};
op_nivel4 ::= MODULO: op
    {: RESULT = new StringLocalizado("%", op.fila(), op.col()); :};

op_nivel5 ::= RESTA: op
    {: RESULT = new StringLocalizado("-", op.fila(), op.col()); :};
op_nivel5 ::= NOT: op
    {: RESULT = new StringLocalizado("not", op.fila(), op.col()); :};

opnd ::= LITERAL_ENTERO: num
  {: RESULT = (Exp) sem.literal_ent(num.str()).ponFila(num.fila()).ponCol(num.col()); :}; 
opnd ::= LITERAL_REAL: num
  {: RESULT = (Exp) sem.literal_real(num.str()).ponFila(num.fila()).ponCol(num.col()); :};
opnd ::= TRUE: tr
   {: RESULT = (Exp)sem.true_().ponFila(tr.fila()).ponCol(tr.col()); :};
opnd ::= FALSE: fl
   {: RESULT = (Exp)sem.false_().ponFila(fl.fila()).ponCol(fl.col()); :};
opnd ::= LITERAL_CADENA: id
    {: RESULT = (Exp) sem.literal_cadena(id.str()).ponFila(id.fila()).ponCol(id.col()); :};
opnd ::= IDENTIFICADOR: id
    {: RESULT = (Exp) sem.id(id.str()).ponFila(id.fila()).ponCol(id.col()); :};
opnd ::= NULL: nul
   {: RESULT = (Exp)sem.nulo().ponFila(nul.fila()).ponCol(nul.col()); :};